-- Grammar of Vehicle

comment "--";
comment "{-" "-}";

-- * Tokens

-- NOTE: Token rules are tried IN ORDER, so the order matters!

position token TokArrow    {"->"};
position token TokForall   {"forall"};
position token TokExists   {"exists"};
position token TokIf       {"if"};
position token TokThen     {"then"};
position token TokElse     {"else"};
position token TokDot      {"."};
position token TokElemOf   {":"};
position token TokLambda   {"\\"};

position token TokAll      {"all"};
position token TokAny      {"any"};
position token TokImpl     {"=>"};
position token TokAnd      {"&&"};
position token TokOr       {"||"};
position token TokEq       {"=="};
position token TokNeq      {"!="};
position token TokLe       {"<="};
position token TokLt       {"<"};
position token TokGe       {">="};
position token TokGt       {">"};
position token TokMul      {"*"};
position token TokDiv      {"/"};
position token TokAdd      {"+"};
position token TokSub      {"-"};
position token TokNot      {"~"};
position token TokAt       {"!"};

position token TokType     {"Type"};
position token TokDim      {"Dim"};
position token TokTensor   {"Tensor"};
position token TokReal     {"Real"};
position token TokInt      {"Int"};
position token TokBool     {"Bool"};
position token TokTrue     {"True"};
position token TokFalse    {"False"};
position token TokList     {"List"};
position token TokNil      {"Nil"};
position token TokCons     {"::"};
position token TokSeqOpen  {"["};
position token TokSeqClose {"]"};

position token Name        (lower (letter | digit | '_')*) ;

separator Name "";


-- * Kinds

-- Core structure.
KApp.  Kind  ::= Kind Kind1;

-- Primitive kinds.
KType. Kind1 ::= TokType;
KDim.  Kind1 ::= TokDim;
KList. Kind1 ::= TokList;

coercions Kind 1;


-- * Types

-- Core structure.
TForall.  Type  ::= TokForall [Name] TokDot Type;
TApp.     Type2 ::= Type2 Type3;
TVar.     Type3 ::= Name;

-- Primitive types.
TFun.     Type  ::= Type1 TokArrow Type;
TBool.    Type3 ::= TokBool;
TReal.    Type3 ::= TokReal;
TInt.     Type3 ::= TokInt;
TTensor.  Type3 ::= TokTensor;

-- Type-level dimensions.
TAdd.     Type1 ::= Type1 TokAdd Type2;
TLitDim.  Type3 ::= Integer; -- confusingly, this means natural

-- Type-level lists.
TNil.     Type3 ::= TokNil;
TCons.    Type1 ::= Type1 TokCons Type;
TLitList. Type3 ::= TokSeqOpen [Type] TokSeqClose;

coercions Type 4;

separator Type ",";


-- * Expressions

-- NOTE:
--
--   The syntax for expressions used by the parser is more general than that
--   permitted by our bidirectional typing, which allows us to emit an more
--   specific error message when we're missing a type annotation, rather than
--   emitting a parse error.
--

layout "let";
layout stop "in";

-- Core structure.
EAnn.       Expr   ::= Expr1 TokElemOf Type;
ELet.       Expr1  ::= "let" "{" [Decl] "}" "in" Expr1;
ELam.       Expr2  ::= TokLambda [Name] TokArrow Expr2;
EApp.       Expr11 ::= Expr11 Expr12;
EVar.       Expr12 ::= Name;

-- NOTE: these aren't part of the surface syntax.
internal ETyApp. Expr ::= Expr Type;
internal ETyLam. Expr ::= TokLambda "{" [Name] "}" TokArrow Expr;

-- Conditional expressions.
EIf.        Expr1  ::= TokIf Expr2 TokThen Expr2 TokElse Expr2;
EImpl.      Expr3  ::= Expr4 TokImpl Expr3;
EAnd.       Expr4  ::= Expr5 TokAnd Expr4;
EOr.        Expr5  ::= Expr6 TokOr Expr5;
ENot.       Expr9  ::= TokNot Expr9;
ETrue.      Expr12 ::= TokTrue;
EFalse.     Expr12 ::= TokFalse;

-- Integers and reals.
EEq.        Expr6  ::= Expr7 TokEq Expr7;
ENeq.       Expr6  ::= Expr7 TokNeq Expr7;
ELe.        Expr6  ::= Expr7 TokLe Expr7;
ELt.        Expr6  ::= Expr7 TokLt Expr7;
EGe.        Expr6  ::= Expr7 TokGe Expr7;
EGt.        Expr6  ::= Expr7 TokGt Expr7;
EMul.       Expr7  ::= Expr7 TokMul Expr8;
EDiv.       Expr7  ::= Expr7 TokDiv Expr8;
EAdd.       Expr8  ::= Expr8 TokAdd Expr9;
ESub.       Expr8  ::= Expr8 TokSub Expr9;
ENeg.       Expr9  ::= TokSub Expr9;
ELitInt.    Expr12 ::= Integer;
ELitReal.   Expr12 ::= Double;

-- Tensors.
EAt.        Expr10 ::= Expr10 TokAt Expr11;
EAll.       Expr12 ::= TokAll;
EAny.       Expr12 ::= TokAny;
ELitTensor. Expr12 ::= TokSeqOpen [Expr] TokSeqClose;

coercions Expr 12;

separator Expr ",";


-- * Declarations

DeclNetw.   Decl ::= "network" Name TokElemOf Type;
DeclData.   Decl ::= "dataset" Name TokElemOf Type;
DefType.    Decl ::= Name [Name] "=" Type;
DefFunType. Decl ::= Name TokElemOf Type;
DefFunExpr. Decl ::= Name [Name] "=" Expr;

separator Decl ";";


-- * Program

layout toplevel;

Main. Prog ::= [Decl]
