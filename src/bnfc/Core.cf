-- Grammar of Vehicle Core

comment "--";
comment "{-" "-}";

-- * Tokens

position token Builtin
  ( {"all"} | {"any"}
	| {"=>"} | {"&&"} | {"||"}
	| {"=="} | {"!="} | {"<="} | {"<"} | {">="} | {">"}
	| {"*"} | {"/"} | {"+"} | {"-"} | {"~"} | {"!"}
	| {"Type"} | {"Tensor"} | {"Real"} | {"Nat"}
	| {"Bool"} | {"True"} | {"False"}
	| {"List"} | {"Nil"} | {"Cons"});

position token Name (lower (letter | digit | '_')*) ;

-- * Kinds

KApp. Kind  ::= Type Type1;
KCon. Kind1 ::= Builtin;

coercions Kind 1;


-- * Types

TApp.    Type  ::= Type Type1;
TForall. Type  ::= "forall" "(" Name Kind1 ")" Type1;
TVar.    Type1 ::= Name;
TCon.    Type1 ::= Builtin;
TLitNat. Type1 ::= Integer; -- confusingly, this means natural;

coercions Type 1;


-- * Expressions

EApp.     Expr  ::= Expr Expr1;
ELam.     Expr  ::= "lambda" "(" Name Type1 ")" Expr1;
ETyApp.   Expr  ::= Expr "{" Type "}";
ETyLam.   Expr  ::= "lambda" "{" Name Kind1 "}" Expr1;
ELet.     Expr  ::= "let" "(" Name Type ")" Expr1 Expr1;
EAnn.     Expr  ::= Expr  ":type" Type1;
EVar.     Expr1 ::= Name;
ECon.     Expr1 ::= Builtin;
ELitNat.  Expr1 ::= Integer;
ELitReal. Expr1 ::= Double;

coercions Expr 1;


-- * Declarations

DeclNetw. Decl ::= "(" "declare-network" Name Type1 ")";
DeclExpr. Decl ::= "(" "define-fun" Name Type1 Expr1 ")";

separator nonempty Decl "";

-- * Program

Main. Prog ::= "(" [Decl] ")";
